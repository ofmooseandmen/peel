plugins {
    id 'application'
    id 'base'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'eclipse'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}
apply from: 'resources/eclipse/eclipse.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation(
        'com.github.ofmooseandmen.wire:wire-app:2020.07-SNAPSHOT',
        'com.github.ofmooseandmen.wire:wire-device:2020.07-SNAPSHOT',
        'com.github.ofmooseandmen.wire:wire-discovery:2020.07-SNAPSHOT',
        'com.github.ofmooseandmen.wire:wire-media:2020.07-SNAPSHOT',
        'io.mikael:urlbuilder:2.0.9'
    )
}

application {
    mainClassName = 'io.omam.peel.Peel'
}

javafx {
    version = '14'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

if (Integer.valueOf(JavaVersion.current().getMajorVersion()) > 11) {
    // --release 11: configures the compiler to produce class files that will link against version 11
    compileJava.options.compilerArgs += ['--release', '11']
}

compileJava.options.encoding = 'UTF-8'
// See: https://docs.oracle.com/javase/9/tools/javac.htm#JSWOR627
compileJava.options.compilerArgs += [
    '-Xlint:cast',
    '-Xlint:classfile',
    '-Xlint:deprecation',
    '-Xlint:dep-ann',
    '-Xlint:divzero',
    '-Xlint:empty',
    '-Xlint:fallthrough',
    '-Xlint:finally',
    '-Xlint:overrides',
    '-Xlint:path',
    '-Xlint:processing',
    '-Xlint:rawtypes',
    '-Xlint:serial',
    '-Xlint:static',
    '-Xlint:unchecked',
    '-Xlint:varargs',
    '-Xlint:-options',
    '-Werror'
]

jar {
    /* include source */
    from (sourceSets.main.allSource.getSrcDirTrees())
}

/* Modify the Eclipse project settings. */
eclipse { 
    jdt { file { withProperties eclipsePrefs } } 
}

task copyEclipsePrefs(type: Copy) {
    from rootProject.file('resources/eclipse/org.eclipse.jdt.ui.prefs')
    into "${projectDir}/.settings"
}
tasks.eclipse.finalizedBy copyEclipsePrefs
